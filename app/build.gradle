apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "de.robv.android.xposed.mods.appsettings"
        minSdkVersion 15
        targetSdkVersion 15
        versionName "1.15"
        versionCode 33
        archivesBaseName = "App_Settings-v${versionName}"
    }

    lintOptions{
        disable 'MissingTranslation'
    }

    /*
    If you want to sign your releases, create a file in the root of the project named keystore.properties with this content:
        storePassword=myStorePassword
        keyPassword=mykeyPassword
        keyAlias=myKeyAlias
        storeFile=myStoreFileLocation
     */

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    boolean doSign = keystorePropertiesFile.exists()

    if (doSign) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            if (doSign) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    provided 'de.robv.android.xposed:api:82'
}
